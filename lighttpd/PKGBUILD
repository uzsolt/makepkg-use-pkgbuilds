# $Id: PKGBUILD 185242 2013-05-12 10:58:13Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=lighttpd
pkgver=1.4.32
pkgrel=5
pkgdesc='A secure, fast, compliant and very flexible web-server'
license=('custom')
arch=('i686' 'x86_64')
url="http://www.lighttpd.net/"
depends=('pcre' 'bzip2' 'libldap' 'util-linux' 'systemd' 
    $(useflag use_lua lua51)
    $(useflag use_mysql libmariadbclient)
    sqlite3 libxml2
)
makedepends=('fcgi' 'libxml2' 'e2fsprogs' 'sqlite' 'gdbm' 'pkgconfig')
backup=('etc/lighttpd/lighttpd.conf' 'etc/logrotate.d/lighttpd')
options=('!libtool' 'emptydirs')
install='lighttpd.install'
source=("http://download.lighttpd.net/lighttpd/releases-1.4.x/lighttpd-${pkgver}.tar.xz"
        "http://download.lighttpd.net/lighttpd/releases-1.4.x/lighttpd-${pkgver}.tar.xz.asc"
        'lighttpd.logrotate.d' 'lighttpd.conf' 'lighttpd.tmpfiles' 'lighttpd.service')
sha256sums=('1368f80069ce71f5928cad59c8e60c0b95876942ca9e02c53853e54ae24aedc1'
            'SKIP'
            '41f6c0042bb61021553779f861910e335834f6c15e4411756cdc6233b31076fe'
            'ee56422fe48f2683ccb5ca2e3dc6bad79ea8e1cbd043b21d2ea73b87018e35aa'
            'd8a185145a7c08b4fd8c8e6c12dae3e176389dd9b1c66e239757b2ba5108c871'
            '503fd8557297f4fb814d74effcb59633135b3c2407a87f5abe97467f0822c147')

build() {
	cd $srcdir/$pkgname-$pkgver

	./configure --prefix=/usr \
		--sbindir=/usr/bin \
		--libdir=/usr/lib/lighttpd/ \
		--sysconfdir=/etc/lighttpd \
		--with-ldap \
		--with-attr \
		--with-openssl \
		--with-kerberos5 \
		--without-fam \
		--with-gdbm \
		--with-memcache \
        --with-webdav-props --with-webdav-locks \
        $(useflag use_lua --with{,out}-lua ) \
        $(useflag use_mysql --with{,out}-mysql )
	make
}

check() {
	cd $srcdir/$pkgname-$pkgver
	make check
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install

	install -D -m644 $srcdir/lighttpd.logrotate.d $pkgdir/etc/logrotate.d/lighttpd
	install -D -m644 $srcdir/lighttpd.conf $pkgdir/etc/lighttpd/lighttpd.conf
	install -d -m755 -o http -g http $pkgdir/var/{log,cache}/lighttpd/
	install -D -m644 ${srcdir}/lighttpd.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/lighttpd.conf
	install -D -m644 ${srcdir}/lighttpd.service ${pkgdir}/usr/lib/systemd/system/lighttpd.service

	pushd doc/config >/dev/null
	find . -type f ! -name 'Makefile*' -exec install -D -m644 {} ${pkgdir}/usr/share/doc/lighttpd/config/{} \;
	popd >/dev/null

	install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
